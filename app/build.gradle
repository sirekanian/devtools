apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    namespace 'com.sirekanyan.devtools'
    compileSdkVersion 33
    defaultConfig {
        applicationId "com.sirekanyan.devtools"
        minSdkVersion 17
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt')
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.core:core-ktx:1.9.0'
}

afterEvaluate {
    def grantSystemPermissions = {
        exec {
            def defaultPackage = android.defaultConfig.applicationId
            def debugSuffix = android.buildTypes['debug'].applicationIdSuffix
            commandLine('./grant.sh', defaultPackage + debugSuffix)
        }
    }
    assembleDebug.doLast { grantSystemPermissions() }
    installDebug.doLast { grantSystemPermissions() }
}
